#!/usr/bin/python3.7
# -*- coding: utf-8 -*-

"""
This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <https://www.gnu.org/licenses/>.
"""

import os

workdir: config["workdir"]
singularity: config["singularity_docker_image"]
localrule: copy


rule RNASeq_target:
    input:
        copy = config["sample"].keys(),
        source = config["sources"].keys(),
        quant = expand(
            "salmon/{sample}/quant.sf",
            sample=config["pair"].keys()
        ),
        saindex = "salmon/transcriptome_index",
        fastqc = expand(
            "fastqc/{sample}_fastqc.html",
            sample=config["sample"].keys()
        ),
        multiqc = "multiqc/report.html"
    message:
        "Finishing the Slamon pipeline"


def fq_pairs(wildcards):
    return config["pair"][wildcards.sample]


rule copy_fastq:
    input:
        lambda wildcards: config["sample"][wildcards.sample]
    output:
        temp("{sample}")
    message:
        "Copying {wildcards.sample} for further process"
    resources:
        mem_mb = (
            lambda wildcards, attempt: min(attempt * 128, 512)
        )
    wildcard_constraints:
        sample = r"[^/]+"
    threads: 1
    params:
        extra = "",
        cold_storage = config["cold_storage"]
    wrapper:
        "https://raw.githubusercontent.com/tdayris/yawn/master/SnakemakeWrappers/cp/8.25"


rule copy_sources:
    input:
        lambda wildcards: config["sources"][wildcards.source]
    output:
        temp("{source}")
    message:
        "Copying {wildcards.source} for indexing"
    resources:
        mem_mb = (
            lambda wildcards, attempt: min(attempt * 128, 512)
        )
    wildcard_constraints:
        source = r"[^/]+"
    threads: 1
    params:
        extra = "",
        cold_storage = config["cold_storage"]
    wrapper:
        "https://raw.githubusercontent.com/tdayris/yawn/master/SnakemakeWrappers/cp/8.25"


rule salmon_index:
    input:
        config["fasta_transcriptome"]
    output:
        directory("salmon/transcriptome_index")
    log:
        "logs/salmon/transcriptome_index.log"
    message:
        "Indexing Transcriptome"
    resources:
        mem_mb = (
            lambda wildcards, attempt: min(attempt * 8192, 20480)
        )
    threads:
        min(config["threads"], 8)
    params:
        extra = config.get("salmon_index_extra", "")
    wrapper:
        "master/bio/salmon/index"


rule salmon_quant:
    input:
        unpack(fq_pairs),
        index = ancient("salmon/transcriptome_index")
    output:
        quant = "salmon/{sample}/quant.sf",
        quant_genes = "salmon/{sample}/quant.genes.sf",
        cmd = "salmon/{sample}/cmd_info.json",
        lib = "salmon/{sample}/lib_format_counts.json",
        aux_info = directory("salmon/{sample}/aux_info"),
        lib_params = directory("salmon/{sample}/libParams"),
        logs = directory("salmon/{sample}/logs")
    log:
        "logs/salmon/quant_{sample}.log"
    message:
        "Quantification of {wildcards.sample} with salmon"
    threads:
        min(config["threads"], 12)
    priority:
        50
    params:
        libtype = config["libtype"],
        extra = config.get("salmon_quant_extra", "")
    resources:
        mem_mb = (
            lambda wildcards, attempt: min(attempt * 8192, 20480)
        )
    wrapper:
        "master/bio/salmon/quant"

rule fastqc:
    input:
        "{sample}"
    output:
        html = "fastqc/{sample}_fastqc.html",
        zip = "fastqc/{sample}_fastqc.zip"
    params: ""
    threads: 1
    resources:
        mem_mb = (
            lambda wildcards, attempt: min(attempt * 256, 768)
        )
    log:
        "logs/fastqc/{sample}.log"
    message:
        "Controling quality of {wildcards.sample}"
    wrapper:
        "master/bio/fastqc"


rule multiqc:
    input:
        html = expand("fastqc/{sample}_fastqc.html",
                      sample=config["sample"].keys()),
        zip = expand("fastqc/{sample}_fastqc.zip",
                     sample=config["sample"].keys()),
        cmd = expand("salmon/{sample}/cmd_info.json",
                     sample=config["pair"].keys()),
        lib = expand("salmon/{sample}/lib_format_counts.json",
                     sample=config["pair"].keys())
    output:
        "multiqc/report.html"
    params: ""
    threads: 1
    resources:
        mem_mb = (
            lambda wildcards, attempt: min(attempt * 256, 768)
        )
    log:
        "logs/multiqc.log"
    message:
        "Gathering quality reports"
    wrapper:
        "master/bio/multiqc"
